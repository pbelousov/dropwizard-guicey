plugins {
    id 'com.github.ben-manes.versions' version '0.46.0'
    id 'com.github.dkorotych.gradle-maven-exec' version '3.0' apply false
}

wrapper {
    gradleVersion = '8.3'
}

description = 'Dropwizard guicey examples'

ext {
    guiceyBom = '6.1.1'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'project-report'

    sourceCompatibility = 11

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    repositories { mavenLocal(); mavenCentral(); maven { url 'https://jitpack.io' }}
    dependencies {
        implementation platform("ru.vyarus.guicey:guicey-bom:$guiceyBom")

        constraints {
            implementation 'io.github.dropwizard-jobs:dropwizard-jobs-core:5.1.2'

            implementation 'org.flywaydb:flyway-core:9.17.0'
            // flyway not compatible with h2 1.4.200 anymore
            implementation 'com.h2database:h2:2.1.214'
        }

        implementation 'ru.vyarus:dropwizard-guicey'

        testImplementation 'ru.vyarus:spock-junit5:1.2.0'
        testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
        testImplementation "io.dropwizard:dropwizard-testing"
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "skipped", "failed"
            exceptionFormat "full"
        }
        maxHeapSize = "512m"
    }

}

dependencyUpdates.revision = 'release'
