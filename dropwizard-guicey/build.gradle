plugins {
    id 'java-library'
    id 'ru.vyarus.mkdocs'
}

dependencies {
    provided 'org.junit.jupiter:junit-jupiter-api'
    provided 'io.dropwizard:dropwizard-testing'
    provided "org.glassfish.hk2:guice-bridge"

    api ('com.google.inject:guice') {
        // use dropwizard guava version
        exclude group: 'com.google.guava', module: 'guava'
        // jakarta-inject used instead
        exclude group: 'javax.inject', module: 'javax.inject'
    }
    api 'com.google.inject.extensions:guice-servlet'
    api 'io.dropwizard:dropwizard-core'
    api 'ru.vyarus:generics-resolver:3.0.3'

    testImplementation 'org.glassfish.jersey.inject:jersey-hk2'
    testImplementation 'io.dropwizard:dropwizard-auth'
    testImplementation 'org.glassfish.jersey.ext:jersey-proxy-client'
    testImplementation 'org.junit.platform:junit-platform-testkit'
    testImplementation 'uk.org.webcompere:system-stubs-jupiter:2.0.2'
    testImplementation 'com.google.truth:truth:1.1.3'

    // required for pure junit 5 tests
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter'
}

// core module group is different!
group = 'ru.vyarus'
description = 'Dropwizard guice integration'

javaLib {
    // java 9 auto module name
    autoModuleName = 'ru.vyarus.dropwizard.guicey'
    // use only direct dependencies in the generated pom, removing BOM mentions
    pom.removeDependencyManagement()
}

mkdocs {
    publish {
        docPath = '5.7.1'
        rootRedirect = true
        rootRedirectTo = 'latest'
        versionAliases = ['latest']
    }
    extras = [
            'version': '5.7.1',
            'ext': '5.7.1-1',
            'dropwizard': rootProject.dropwizard,
            'guice': rootProject.guice
    ]
}